apply plugin: 'com.android.application'
//apply from: '/Users/cailiming/develop/Android-Plugin-Framework/FairyPlugin/plugin.gradle'
apply from: "https://raw.githubusercontent.com/limpoxe/Android-Plugin-Framework/0.0.61-snapshot/FairyPlugin/plugin.gradle"

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.1"

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    defaultConfig {
        applicationId "com.example.plugintest"
        minSdkVersion 11
        targetSdkVersion 27
        versionCode 1
        versionName "1.0.1"
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        textReport false
        htmlReport false
        xmlReport false
    }

    signingConfigs {
        debug {
            storeFile file("../PluginMain/limpoxe.keystore")
            storePassword "123456"
            keyAlias "limpoxe"
            keyPassword "123456"
        }

        release {
            storeFile file("../PluginMain/limpoxe.keystore")
            storePassword "123456"
            keyAlias "limpoxe"
            keyPassword "123456"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '_' + getDate()
            debuggable true
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            versionNameSuffix '_' + getDate()
            debuggable true
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

//    dataBinding {
//        enabled = true
//    }
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy_MM_dd_HH_mm_ss')
    return formattedDate
}


dependencies {

    implementation project(":Samples:plugintest3");

    //////////////////////////////////////////////////////////////////////
    //下面这一堆provided的配置，是因为这些包在编译宿主的时候，已经被打包到宿主的apk中。
    //所以这里打包非独立插件的时候，要通过provided配置防止这些包被打入非独立插件。
    //如果是独立插件，无需任何特殊配置
    /////////////////////////////////////////////////////////////////////

    compileOnly files(project(':Samples:PluginBase').getBuildDir().absolutePath + '/outputs/apk/PluginBase-debug.jar')

    //支持使用maven坐标配置
    baselinePatch files(project(':Samples:PluginMain').getBuildDir().absolutePath + '/distributions/host.bar')

    //Just For Test ButterKnife
    //compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

}


afterEvaluate {
    tasks.getByName("packageDebug").doLast {
        copy {
            println "复制插件" + apkPathList.get(0).absolutePath + "到宿主assets目录"
            from apkPathList.get(0).getParent()
            include project.name + '-debug.apk'
            into(project(':Samples:PluginMain').getProjectDir().absolutePath + '/src/main/assets/')
        }
    }
}
